module LayoutPage

open Common
open Giraffe
open GiraffeViewEngine

type LayoutModel = { Title: string }

type LayoutSlot =
    { Styles: XmlNode list
      Header: XmlNode list
      Body: XmlNode list
      Sidebar: XmlNode list
      Scripts: XmlNode list
      ModuleName: string }

let linkStyle (href: string) = link [ _href href; _rel "stylesheet" ]

let favicon icon =
    link [ _href icon
           _rel "icon"
           _type "image/png" ]

let favicons =
    [ 16; 32; 96; 160; 192 ]
    |> List.map
        ((fun d -> sprintf "/assets/favicon-%dx%d.png" d d)
         >> favicon)

let view (data: LayoutModel) (slots: LayoutSlot) =
    html [ _lang "zh" ] [
        head
            []
            ([ meta [ _charset "utf-8" ]
               meta [ _lang "zh" ]
               meta [ _name "viewport"
                      _content "width=device-width, initial-scale=1.0" ]
               title [] [
                   rawText (sprintf "%s - ç½‘ä¸Šå†²æµªæŒ‡å—" data.Title)
               ]
               yandexTag
               linkStyle "/dist/theme.css"
               linkStyle "/dist/article.css" ]
             @ favicons @ slots.Styles)
        body
            []
            ([ yandexNoScript
               div [ _class "z-container relative" ] slots.Header
               div [ _class "z-container flex sm:flex-row flex-col flex-nowrap" ] [
                   div [ _class "module" ] slots.Body
                   div [ _class "z-side-items" ] slots.Sidebar
               ]
               div [ _class "z-container font-mono text-sm mt-20 mb-10" ] [
                   div [] [
                       p [] [
                           rawText "æœ¬ç½‘ç«™æ‰€å±•ç¤ºçš„æ–‡ç« ç”± Zeeko Zhu é‡‡ç”¨çŸ¥è¯†å…±äº«ç½²å-ç›¸åŒæ–¹å¼å…±äº« 4.0 å›½é™…è®¸å¯åè®®è¿›è¡Œè®¸å¯"
                       ]
                       p [] [
                           rawText "Zeeko's blog, Powered by ASP.NET Core ğŸ³"
                       ]
                   ]
               ]
               script [ _src "/lib/zepto.min.js" ] [] ]
             @ slots.Scripts)
    ]
